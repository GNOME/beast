dnl ************************************
dnl *** include special BEAST macros ***
dnl ************************************
builtin(include, acbeast.m4)dnl

#
# Configure script for BSE-PortAudio
#
AC_INIT
AC_CONFIG_SRCDIR([bsepcmdevice-portaudio.cc])
AM_CONFIG_HEADER(configure.h)
AC_PREREQ(2.57)

#
# Package version numbers
#
AC_DIVERT_BEFORE_HELP([
PKG_MAJOR_VERSION=0
PKG_MINOR_VERSION=6
PKG_MICRO_VERSION=4
PKG_INTERFACE_AGE=0
PKG_BINARY_AGE=0
])
dnl#
dnl# Making releases:
dnl#   PKG_MICRO_VERSION += 1;
dnl#   PKG_INTERFACE_AGE += 1;
dnl#   PKG_BINARY_AGE += 1;
dnl# if any functions have been added, set PKG_INTERFACE_AGE to 0.
dnl# if backwards compatibility has been broken,
dnl# set PKG_BINARY_AGE _and_ PKG_INTERFACE_AGE to 0.
dnl# PKG_RELEASE_CANDIDATE may contain rc[0-9]+ or nothing
PKG_RELEASE_CANDIDATE= #rc1

# version number setup
PKG_VERSION=$PKG_MAJOR_VERSION.$PKG_MINOR_VERSION.$PKG_MICRO_VERSION
if test -n "$PKG_RELEASE_CANDIDATE" ; then
    PKG_VERSION=$PKG_VERSION-$PKG_RELEASE_CANDIDATE
fi
AC_SUBST(PKG_MAJOR_VERSION)
AC_SUBST(PKG_MINOR_VERSION)
AC_SUBST(PKG_MICRO_VERSION)
AC_SUBST(PKG_INTERFACE_AGE)
AC_SUBST(PKG_BINARY_AGE)
AC_SUBST(PKG_VERSION)

# compute libtool versions
LT_RELEASE=$PKG_MAJOR_VERSION.$PKG_MINOR_VERSION
LT_CURRENT=`expr $PKG_MICRO_VERSION - $PKG_INTERFACE_AGE`
LT_REVISION=$PKG_INTERFACE_AGE
LT_AGE=`expr $PKG_BINARY_AGE - $PKG_INTERFACE_AGE`
AC_SUBST(LT_RELEASE)
AC_SUBST(LT_CURRENT)
AC_SUBST(LT_REVISION)
AC_SUBST(LT_AGE)

dnl figure debugging default, prior to $ac_help setup
dnl
AC_DIVERT_BEFORE_HELP([
if test `expr $PKG_MINOR_VERSION \% 2 \| $PKG_MAJOR_VERSION \< 1` = 1 ; then
        debug_default=yes
else
        debug_default=minimum
fi
])


# initialize automake, don't define PACKAGE and VERSION
AM_INIT_AUTOMAKE(bse-portaudio, $PKG_VERSION, no-define)

# in case we have subdirectories.
AC_PROG_MAKE_SET
# Disable automatic maintainer mode.
AM_MAINTAINER_MODE

# architecture information
AC_CANONICAL_TARGET
AC_DEFINE_UNQUOTED(PKG_ARCH_NAME, "$target", [Architecture Description])

# declare --enable-* args and collect ac_help strings
AC_ARG_ENABLE(debug,       [  --enable-debug=[no/minimum/yes] turn on debugging [default=$debug_default]],,enable_debug=$debug_default)
AC_ARG_ENABLE(devel-rules, [  --enable-devel-rules=[no/yes]   turn on developer build rules],,)
# $enable_devel_rules

dnl setup DEBUG defaults
if test "x$enable_debug" = "xyes"; then
  DEBUG_FLAGS="-g -DG_ENABLE_DEBUG"
else if test "x$enable_debug" = "xno"; then
  DEBUG_FLAGS="-DG_DISABLE_CHECKS -DG_DISABLE_CAST_CHECKS" # -DG_DISABLE_ASSERT
else
  DEBUG_FLAGS="-DG_DISABLE_CAST_CHECKS"
fi fi
if test -z "$CFLAGS"; then
  CFLAGS="$DEBUG_FLAGS"
else
  CFLAGS="$CFLAGS $DEBUG_FLAGS"
fi
if test -z "$CXXFLAGS"; then
  CXXFLAGS="$DEBUG_FLAGS"
else
  CXXFLAGS="$CXXFLAGS $DEBUG_FLAGS"
fi

dnl Foreign defaults:
MC_IF_VAR_EQ(enable_static, yes, , enable_static=no)
MC_IF_VAR_EQ(enable_shared, no, , enable_shared=yes)


dnl
dnl Define package requirements.
dnl
AC_DEFUN(AC_LIBBSE_CHECK,
[
    dnl We need BSE, GLib, etc.
    PKG_CHECK_MODULES(BSE, bse >= 0.6.4)
    AC_SUBST(BSE_CFLAGS)
    AC_SUBST(BSE_LIBS)

    dnl --- BSE Drivers directory ---
    AC_MSG_CHECKING([for BSE Driver installation directory])
    bsedriverinstalldir=`$PKG_CONFIG --variable=driverdir bse`
    AC_MSG_RESULT([$bsedriverinstalldir])
    if test -z "$bsedriverinstalldir" ; then
      AC_MSG_ERROR([failed to find installation directory])
    fi
    AC_SUBST(bsedriverinstalldir)
])
AC_DEFUN(AC_PORTAUDIO_CHECK,
[
    saved_cflags="$CFLAGS"
    saved_cxxflags="$CXXFLAGS"
    saved_ldflags="$LDFLAGS"
    CFLAGS="$CFLAGS $BSE_CFLAGS"
    CXXFLAGS="$CFLAGS $BSE_CFLAGS"
    LDFLAGS="$LDFLAGS $BSE_LIBS"
    AC_LANG_SAVE
    AC_LANG_CPLUSPLUS

    dnl --- find portaudio.h ---
    found_portaudio=0
    AC_CHECK_HEADERS(portaudio.h, found_portaudio=1)
    if test $found_portaudio = 0 ; then
        AC_MSG_ERROR([failed to detect portaudio.h, PortAudio not present?])
    fi

    dnl --- check for PortAudio >= 19 ---
    AC_MSG_CHECKING([for PortAudio with version >= 19])
    found_portaudio19=0
    AC_TRY_COMPILE([#include <portaudio.h>],[
            Pa_WriteStream (0, 0, 0); /* we use the blocking API, which is available since V19 */
    ], [ found_portaudio19=1; AC_MSG_RESULT(present); ], [ AC_MSG_RESULT(not present); ])
    if test $found_portaudio19 = 0; then
	AC_MSG_ERROR([PortAudioV19 is required to build this package])
    fi
    
    dnl --- check for -lportaudio ---
    found_libportaudio=0
    AC_CHECK_LIB(portaudio, Pa_Initialize, PORTAUDIO_LIBS="-lportaudio"; found_libportaudio=1)
    if test $found_libportaudio = 0; then
	AC_MSG_ERROR([PortAudioV19 is required to build this package])
    fi
    AC_SUBST(PORTAUDIO_LIBS)

    AC_LANG_RESTORE
    CFLAGS="$saved_cflags"
    CXXFLAGS="$saved_cxxflags"
    LDFLAGS="$saved_ldflags"
])

# check compilers and their behaviour, setup CFLAGS
MC_PROG_CC_WITH_CFLAGS
MC_PROG_CXX_WITH_CXXFLAGS
AC_PROG_CPP
AC_PROG_CXX
AC_PROG_CXXCPP
AC_C_CONST
AC_C_INLINE
AC_HEADER_STDC

# Checks for programs
AC_PROG_INSTALL
AC_PROG_LN_S

# initialize libtool, we need dynamic module support
AC_LIBTOOL_DLOPEN
AM_PROG_LIBTOOL

# Figure build requirement
AC_MSG_CHECKING(whether make is GNU Make)
if $ac_make --version 2>/dev/null | grep '^GNU Make ' >/dev/null ; then
        AC_MSG_RESULT(yes)
else
        AC_MSG_RESULT(no)
        AC_MSG_ERROR(This package requires GNU Make)
fi

# Check requirement sets
AC_LIBBSE_CHECK
AC_PORTAUDIO_CHECK

# Automake @VARIABLE@ exports.
AC_SUBST(CFLAGS)
AC_SUBST(CXXFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)


# create output files
AC_CONFIG_FILES([
Makefile
])

AC_OUTPUT
